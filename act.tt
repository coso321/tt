#nop ==常用action==;

#nop #act {1分钟内回答以下问题} {answer 1};

set wimpy 30;

#act {^( 你看起来可能有些累了。)} {powerup;};
#act {你动作似乎开始有点不太灵光，但是仍然有条不紊} {xqtic};
#act {你似乎十分疲惫，看来需要好好休息了} {xqtic; #delay 2 lifepfm};
#act {你气喘嘘嘘，看起来状况并不太好} {xqtic};
#act {你看起来已经力不从心了} {xqtic; #delay 2 lifepfm};
#act {^你受伤过重，已经有如风中残烛，随时都可能断气。} {xqtic; #20 {touxiang; halt; quit} };
#act {^( 你已经陷入半昏迷状态，随时都可能摔倒晕去。)} {xqtic; #20 {touxiang; halt; quit}};
#act {^( 你已经一副头重脚轻的模样，正在勉力支撑著不倒下去。)} {xqtic ; #delay 2 lifepfm};
#act {^( 你摇头晃脑、歪歪斜斜地站都站不稳，眼看就要倒在地上。)} {
    #class lifeline open;
    xqtic;
    #act {^你上一次恢复气血，全身真气还没有平缓，请过一会儿再运行真气吧。} {
        #10 {touxiang; halt; quit}
    }; 
    #act {^你深深吸了几口气，脸色看起来好多了。} {
        #delay 2 lifepfm; #class lifeline kill;
    };
    #class lifeline close;
};

#act {^你有很长一段时间没有备份档案了} {backup};

#act {^吊桥还没有升起来，你就这样走了，可能会给外敌可乘之机的。$} {sheng bridge};

#act {^你拿出一两黄金(Gold)给钱老本。$} {ask qian about 进宫};

#act {^你必须先把木门打开！$} {open door};

#act {^你在这里找到了一本武功心得。$} {kan xinde};

#act {^你找到了一些奇怪的线索。$} {mess clue};

#nop #act {^你感觉心中的杀气渐渐消失} {#delay 1 {chanhui}};

#act {^%s中年女佣(Nvyong)} {ask nvyong about 郭大侠};

#act {^ %*文铜板(Coin)$} {get coin;};
#act {^ %*两白银(Silver)$} {get silver;};
#act {^ %*两黄金(Gold)$} {get gold;};
#act {^ %*银票(Cash)$} {get cash;};
#act {^    %*之护腕(Wrists)} {get wrists;};
#act {^    %*之盾(Shield)} {get shield;};
#act {^    %*之腰带(Waist)} {get waist;};
#act {^你从%*{搜出|拿出}一柄长剑。$} {drop changjian};
#act {^你从%*{搜出|拿出}一把青锋剑。$} {drop qingfeng sword};
#act {^你获得了%*份石炭【劣质】。$} {drop tan};
#alias pickitem {
    #act {^{你捡起.*|你附近没有这样东西}} {#unact {^{你捡起.*|你附近没有这样东西}}; #path run};
    #path stop;
};

#nop #act {^你从%*{搜出|拿出}一柄长鞭。$} {drop whip};

#act {^你从%*{除下|拿出|搜出}一件布衣。$} {drop cloth};

#act {^你从%*{搜出|拿出}一只铁轮。$} {drop falun};

#act {^{目前权限|重新连线完毕}} { #delay 1 {hpbrief; sc; reloadmap; log;}};

#act {^≡━━━━◎人物详情◎━━━━━━━━━━━━━━━━━━━━━━━━━━≡$} {
	#class tmp open;
	#act {^ 【%*】%* %*(%*)$} {#var myname %%3; #var myid @lower{%%4}};
	#act {你是%*第%*代弟子} {#var mymenpai %%1};
	#act {你是朝廷%*品官员} {#var mymenpai 朝廷};
	#act {你是一位%*岁的%*婚%*性人类} {#var myage @ctd{%%1};#var mygender %%3};
	#act {^≡━━━━━━━━━━━━━━━━━━━━━━━━━◎北大侠客行◎━━━━≡} {#class tmp kill};
	#class tmp close;
};


#act {也许是上天垂青于你的勤奋和努力吧，一个小时之内你不用担心被判为机器人。} {#class mxp kill; loadmap};

#act {^{花雕|牛皮}酒袋已经被喝得一滴也不剩了。} {drop jiudai};
