#class mapper open;

#map read map.tt;
#read mapper-paths.tt;

#alias setarea {
    #var roomarea %1;
};

#alias getmapname {
    #class getmapname open;
    #action {^%%1 -} {
        #map set roomname %%1;
        #map set roomarea $areaname;
        #map info;
        #class getmapname kill;
    }
    #class getmapname close;
    %1;
};

#alias maphere {
    #map get roomvnum roomvnum;
    #map get roomname roomname;
    #map get roomarea roomarea;
    #map get roomdesc roomdesc;
    #if {"%1" == "show"} {
        #showme <fca>Room vnum: $roomvnum;
        #showme <fca>Room name: $roomname;
        #showme <fca>Room area: $roomarea;
        #showme <fca>Room desc: $roomdesc;
    };
};

#alias loadmap {
    #class loadmap open;
    #action {^%%1 -} {
        #map goto %%1;
        maphere;
        #if {"$roomname" != ""} {
            maphere show;
        };
    };
    #action {船老大 何博康(He bokang)} {
        #map goto 长江北岸渡口;
        maphere show;
    };
    #action {    船老大 孙浑(Sun hun)} {
        #map goto 长江南岸渡口;
        maphere show;
    };
    #action {    船老大 施顺(Shi shun)} {
        #map goto 黄河南岸渡口;
        maphere show;
    };
    #action {船老大 蒋鼎民(Jiang dingmin)} {
        #map goto 黄河北岸渡口;
        maphere show;
    };
    #action {这里是南昌城的城中心} {
        #map goto 南昌城中心;
        maphere show;
    };
    #action {这里是襄阳的城中心。} {
        #map goto 襄阳城中心;
        maphere show;
    };
    #action {这里是信阳城的中心} {
        #map goto 信阳小广场;
        maphere show;
    };
    #action {泉州是一个安静的港城。} {
        #map goto 泉州城中心;
        maphere show;
    };
    #action {这里是西夏都城灵州的中心广场} {
        #map goto 灵州城中心;
        maphere show;
    };
    #action {Room name: %*} {
        #class loadmap kill;
    };
    #class loadmap close;
    #split 10 1;
    #map flag vtmap on;
    l;
};

#alias unloadmap {
    #map leave;
    #unsplit; #split;
};

#alias mapnof { #map flag nofollow on; };
#alias mapf { #map flag nofollow off; };
#alias mapsize {#unsplit; #split %1 1};

    
#alias map {
    #regexp $areaname {{.+}} { #showme 当前地区：$areaname; getmapname %1} {#showme 当前地区为空; maphere; getmapname %1}; 
};

#alias mi {#map info};
#alias mm {#map move %1; mi};
#alias mg {#map goto %1; mi};
#alias ml {#map list %1};
#alias mlarea {#map list {%*} {} {} {%1}};
#alias mldesc {#map list {%*} {} {%*%1%*}};

#alias mapnum {#map flag asciivnums};
#alias mapvoid {#map insert %1 void};
#alias savemap {#map write map.tt;};

#alias run {#map run {%1} {{%2}};};


#alias mazeexit {
    
    #map exit %1 command {
        #class mazewalk open;
        #var fromdir[north] south;
        #var fromdir[west] east;
        #var fromdir[east] west;
        #var fromdir[south] north;
        #var fromdir[up] down;
        #var fromdir[down] up;
        #var fromdir[northup] southdown;
        #var fromdir[westup] eastdown;
        #var fromdir[eastup] westdown;
        #var fromdir[southup] northdown;
        #var fromdir[northdown] southup;
        #var fromdir[westdown] eastup;
        #var fromdir[eastdown] westup;
        #var fromdir[southdown] northup;
        #var fromdir[southeast] northwest;
        #var fromdir[northwest] southeast;
        #var fromdir[southwest] northeast;
        #var fromdir[northeast] southwest;
        #var lastdir %2;
        #var found 0;
        #action {你逃跑失败。} {
            #delay 1 lastdir;
        } 1;
        #action {%3} {
            #showme <fca>到达目标地点：%4;
            #var found 1;
            #map goto %4;
            #class mazewalk kill;
        } 2;
        #action {这里明显的方向有 %%1、%%2 和 %%3。} {
            #showme <fca> 岔路自动停止;
            #var found 1;
            #map goto %4;
            #class mazewalk kill;
        } 3;
        #action {这里明显的方向有 %%1 和 %%2。} {
            #if {$found == 0} {
                #regex %%1 {$fromdir[$lastdir]} {
                    #showme going %%2;
                    #var lastdir %%2;
                    %%2;
                } {
                    #showme going %%1;
                    #var lastdir %%1;
                    %%1;
                };
            };
        };
        #class mazewalk close;
        set brief 1;
        %1;
    };
};

#alias mazeexit-special {
    
    #map exit %1 command {
        #class mazewalk open;
        #var fromdir[north] south;
        #var fromdir[west] east;
        #var fromdir[east] west;
        #var fromdir[south] north;
        #var fromdir[up] down;
        #var fromdir[down] up;
        #var fromdir[northup] southdown;
        #var fromdir[westup] eastdown;
        #var fromdir[eastup] westdown;
        #var fromdir[southup] northdown;
        #var fromdir[northdown] southup;
        #var fromdir[westdown] eastup;
        #var fromdir[eastdown] westup;
        #var fromdir[southdown] northup;
        #var fromdir[southeast] northwest;
        #var fromdir[northwest] southeast;
        #var fromdir[southwest] northeast;
        #var fromdir[northeast] southwest;
        #var lastdir %2;
        #var found 0;
        
        #action {你逃跑失败。} {
            #delay 1 lastdir;
        } 1;
        #action {%3} {
            #showme <fca>到达目标地点：%4;
            #var found 1;
            #map goto %4;
            #unticker walkmaze;
            set brief 1;
            #class mazewalk kill;
        } 2;
        #action {这里明显的方向有 %%1、%%2 和 %%3。} {
            #showme <fca> 岔路自动停止;
            #var found 1;
            #map goto %4;
            #unticker walkmaze;
            set brief 1;
            #class mazewalk kill;
        } 3;
        #action {这里明显的方向有 %%1 和 %%2。} {
            #if {$found == 0} {
                #regex %%1 {$fromdir[$lastdir]} {
                    #showme going %%2;
                    #var lastdir %%2;
                    %%2;
                } {
                    #showme going %%1;
                    #var lastdir %%1;
                    %%1;
                };
            };
        };
        #class mazewalk close;
        set brief 3;
        %1;
        #ticker walkmaze {l} 0.6;
    };
};


#class mapper close;
