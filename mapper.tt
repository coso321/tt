#class mapper open;


#map read map.tt;
#read mapper-paths.tt;

#alias setarea {
    #action {^【%%1地图%*】} {#var roomarea %%1; #unaction {^【%*地图%*】}};
    lm; q;
};

#alias maphere {
    #map get roomvnum roomvnum;
    #map get roomname roomname;
    #map get roomarea roomarea;
    #map get roomdesc roomdesc;
    #map get roomnote roomnote;
    #showme <fca>Room vnum: $roomvnum;
    #showme <fca>Room name: $roomname;
    #showme <fca>Room area: $roomarea;
    #showme <fca>Room desc: $roomdesc;
    #showme <fca>Room note: $roomnote;
};

#alias mapdone {#map roomflag static on};

#var mapdesc {
    {^    {(?!(「|这里(唯一|明显)的(方向|出口)有)|.*正盘膝坐在地下|.*脸上神色平和，身形凝重，稳如山岳。).*。.*}}{%0}
    {^    船老大 %1(%*)}{船老大 %1}
    {^┌───%1─}{%1}
    {^这条路%1}{这条路%1}
};

#alias mapaction {
    #action {%1} {%2} 1;
};
    
#alias getmapdesc {
    #class mapaction open;
    #action {^    你可以看看(look):%*<node>} {l <node>};
    #action {^    你可以看看(look):lupai} {l lupai};
    #foreach {*mapdesc[%*]} {desc} {mapaction {$desc} {%1 {@@replace{{${mapdesc[$desc]}} {    } {}}}}};
    #class mapaction close;
};

#alias loadmap {
    #map goto 1;
    #var roomname {};
    #class loadmap open;
    #gag {^>};
    #gag {^$};
    #action {^%%1 -} {
        #map list {%%1} {variable} {vnums};
        #if {"%%1" == "%*的储物柜"} {#map goto 644; maphere};  
        #elseif {&{vnums[]} == 1} {#map goto %%1; maphere};
        #elseif {&{vnums[]} == 0} {#showme <fca>房间不存在，将在移动后重试。};
        #else { 
            getmapdesc {mg %%1};
            #delay 1 #class mapaction kill;
        };
    };
    #action {Room name: %S} {#class mapaction kill; #class loadmap kill;};
    #class loadmap close;
    #split 10 2;
    #map flag vtmap on;
    l;
};

#alias unloadmap {
    #map leave;
    #unsplit; #split;
};

#alias mapsize {#unsplit; #split %1 2};

    
#alias map {
    #class tmp open;
    #gag {^>};
    #gag {^$};
    #action {^%%1 -} {
        #map set roomarea $roomarea;
        #map set roomname %%1;
        getmapdesc {mapdesc};
    };
    #class tmp close;
    setarea;
    l;
    #delay 1 {#class mapaction kill; #class tmp kill;};
};

#alias mapdesc {
    #map get roomdesc roomdesc;
    #if {"$roomdesc" == ""} {#map set roomdesc %1;maphere};
    #else {
       #regexp {$roomdesc} {{.*}%1{.*}} {maphere} {#map set roomdesc $roomdesc|%1; maphere};
    };
};

#alias mapnote {
    #map get roomnote roomnote;
    #if {"$roomnote" == ""} {
        #map set roomnote %1;
    } {
        #map set roomnote $roomnote|%1;
    };
    maphere;
};

#alias mi {#map info};
#alias mm {#map move %1; mi};
#alias mg {
    #if {"%1" == "{\d+}"} {#map goto %1};
    #else {#map goto {%1%*} {} {%*%2%*} {%3%*}};
    maphere;
};

#alias ml {#map list {%*%1%*} {} {%*%2%*} {%3%*}};
#alias mldesc {#map list {%*} {} {%*%1%*}};
#alias mlarea {
    #if {"%1" == ""} {maphere; #if {"$roomarea" != "" } {mlarea $roomarea}} {#map list {%*} {} {} {%1%*}};
};

#alias mapnum {#map flag asciivnums};
#alias mapvoid {#map insert %1 void};
#alias savemap {#map write map.tt;};



#alias mazeexit {
    
    #map exit %1 command {
        #class mazewalk open;
        #var fromdir[north] south;
        #var fromdir[west] east;
        #var fromdir[east] west;
        #var fromdir[south] north;
        #var fromdir[up] down;
        #var fromdir[down] up;
        #var fromdir[northup] southdown;
        #var fromdir[westup] eastdown;
        #var fromdir[eastup] westdown;
        #var fromdir[southup] northdown;
        #var fromdir[northdown] southup;
        #var fromdir[westdown] eastup;
        #var fromdir[eastdown] westup;
        #var fromdir[southdown] northup;
        #var fromdir[southeast] northwest;
        #var fromdir[northwest] southeast;
        #var fromdir[southwest] northeast;
        #var fromdir[northeast] southwest;
        #var lastdir %2;
        #var found 0;
        #action {你逃跑失败。} {
            #delay 1 lastdir;
        } 1;
        #action {%3} {
            #showme <fca>到达目标地点：%4;
            #var found 1;
            #map goto %4;
            #class mazewalk kill;
        } 2;
        #action {这里明显的方向有 %%1、%%2 和 %%3。} {
            #showme <fca> 岔路自动停止;
            #var found 1;
            #map goto %4;
            #class mazewalk kill;
        } 3;
        #action {这里明显的方向有 %%1 和 %%2。} {
            #if {$found == 0} {
                #regex %%1 {$fromdir[$lastdir]} {
                    #showme going %%2;
                    #var lastdir %%2;
                    %%2;
                } {
                    #showme going %%1;
                    #var lastdir %%1;
                    %%1;
                };
            };
        };
        #class mazewalk close;
        set brief 1;
        %1;
    };
};

#alias mazeexit-special {
    
    #map exit %1 command {
        #class mazewalk open;
        #var fromdir[north] south;
        #var fromdir[west] east;
        #var fromdir[east] west;
        #var fromdir[south] north;
        #var fromdir[up] down;
        #var fromdir[down] up;
        #var fromdir[northup] southdown;
        #var fromdir[westup] eastdown;
        #var fromdir[eastup] westdown;
        #var fromdir[southup] northdown;
        #var fromdir[northdown] southup;
        #var fromdir[westdown] eastup;
        #var fromdir[eastdown] westup;
        #var fromdir[southdown] northup;
        #var fromdir[southeast] northwest;
        #var fromdir[northwest] southeast;
        #var fromdir[southwest] northeast;
        #var fromdir[northeast] southwest;
        #var lastdir %2;
        #var found 0;
        
        #action {你逃跑失败。} {
            #delay 1 lastdir;
        } 1;
        #action {%3} {
            #showme <fca>到达目标地点：%4;
            #var found 1;
            #map goto %4;
            #unticker walkmaze;
            set brief 1;
            #class mazewalk kill;
        } 2;
        #action {这里明显的方向有 %%1、%%2 和 %%3。} {
            #showme <fca> 岔路自动停止;
            #var found 1;
            #map goto %4;
            #unticker walkmaze;
            set brief 1;
            #class mazewalk kill;
        } 3;
        #action {这里明显的方向有 %%1 和 %%2。} {
            #if {$found == 0} {
                #regex %%1 {$fromdir[$lastdir]} {
                    #showme going %%2;
                    #var lastdir %%2;
                    %%2;
                } {
                    #showme going %%1;
                    #var lastdir %%1;
                    %%1;
                };
            };
        };
        #class mazewalk close;
        set brief 3;
        %1;
        #ticker walkmaze {l} 0.6;
    };
};

#alias guojiang-exit {boat-exit %1 过江};
#alias guohe-exit {boat-exit %1 过河};

#alias boat-exit {
    #map exit %1 command {
        #class guojiang open;
        #action {^你吸了口气，一声“船家”，声音中正平和地远远传了出去。$} {
            #class tmp open;
            #action {^只听得江面上隐隐传来：“别急嘛，这儿正忙着呐……”} {#class tmp kill;#delay {3} {yell boat}};
            #class tmp close;
        };
        #action {^你使出吃奶的力气喊了一声：“船家”$} {
            #class tmp open;
            #action {^只听得江面上隐隐传来：“别急嘛，这儿正忙着呐……”} {#class tmp kill;#delay {3} {yell boat}};
            #class tmp close;
        };
        #var in_boat 0;
        #action {江心驶去。} {
            #var in_boat 1;
        };
        #action {^艄公说“到啦，上岸吧”，随即把一块踏脚板搭上堤岸。} {
            #if {$${in_boat} == 1} {
                #var in_boat 0;
                out;
                maphere;
                #showme <fca>到达目标地点：$$roomname;
                #class guojiang kill;
            };
        };
        #action {正等着你呢，上来吧。} {#class tmp kill;enter};
        #action {艄公将一块踏脚板搭上堤岸，} {#class tmp kill;enter};
        #action {^%*接过你递给的船资} {
            #class tmp open;
            #action {^只听得江面上隐隐传来：“别急嘛，这儿正忙着呐……”} {#class tmp kill;#delay {3} {yell boat}};
            #class tmp close;
        };
        #action {^你的车船通账上还剩%S，这一趟的船资%S。} {
            #class tmp open;
            #action {^只听得江面上隐隐传来：“别急嘛，这儿正忙着呐……”} {#class tmp kill;#delay {3} {yell boat}};
            #class tmp close;
        };
        #class guojiang close;
        ask shao gong about %2;
    };
    #map exitflag %1 avoid on;
};

#alias wait-exit {
    #map exit %1 command {
        #class tmp open;
        #action {%2} {#delay 1 %1};
        #action {%3} {#map flag nofollow off;#showme <fca>等待结束;#class tmp kill};
        #class tmp close;
        %1;
        #map flag nofollow on;
    };
};
#class mapper close;
