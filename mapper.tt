#class mapper open;

#map read map.tt;

#var areaname {}; 

#alias setarea {
    #var areaname %1;
};

#alias getmapname {
    #class getmapname open;
    #action {^%%1 -} {
        #map set roomname %%1;
        #map set roomarea $areaname;
        #map info;
        #class getmapname kill;
    }
    #class getmapname close;
    %1;
};

#alias loadmap {
    #class getmapname open;
    #action {^%%1 -} {
        #map goto %%1;
        #map info;
        #class getmapname kill;
    };
    #class getmapname close;
    #map goto 1;
    #split 10 1;
    #map flag vtmap on;
    #delay 1 l;
};

#alias unloadmap {
    #map leave;
    #unsplit; #split;
};

#alias getarea {
    #map get roomarea $areaname;
};

#alias mapnof { #map flag nofollow on; };
#alias mapf { #map flag nofollow off; };

    
#alias map {
    #regexp $areaname {{.+}} { #showme 当前地区：$areaname; getmapname %1} {#showme 当前地区为空; getarea; getmapname %1}; 
};

#alias mi {#map info};
#alias mm {#map move %1; mi};
#alias mg {#map goto %1; mi};
#alias ml {#map list %1};
#alias mlarea {#map list {%*} {} {} {%1}};

#alias mapnum {#map flag asciivnums};
#alias mapvoid {#map insert %1 void};
#alias savemap {#map write map.tt;};

#alias run {#map run {%1} {{%2}};};


#alias mazeexit {
    
    #map exit %1 command {
        #class mazewalk open;
        #var fromdir[north] south;
        #var fromdir[west] east;
        #var fromdir[east] west;
        #var fromdir[south] north;
        #var fromdir[up] down;
        #var fromdir[down] up;
        #var fromdir[northup] southdown;
        #var fromdir[westup] eastdown;
        #var fromdir[eastup] westdown;
        #var fromdir[southup] northdown;
        #var fromdir[northdown] southup;
        #var fromdir[westdown] eastup;
        #var fromdir[eastdown] westup;
        #var fromdir[southdown] northup;
        #var fromdir[southeast] northwest;
        #var fromdir[northwest] southeast;
        #var fromdir[southwest] northeast;
        #var fromdir[northeast] southwest;
        #var lastdir %2;
        #var found 0;
        #action {你逃跑失败。} {
            #delay 1 lastdir;
        } 1;
        #action {%3} {
            #showme <fca>到达目标地点：%4;
            #var found 1;
            #map goto %4;
            #class mazewalk kill;
        } 2;
        #action {这里明显的方向有 %%1、%%2 和 %%3。} {
            #showme <fca> 岔路自动停止;
            #var found 1;
            #map goto %4;
            #class mazewalk kill;
        } 3;
        #action {这里明显的方向有 %%1 和 %%2。} {
            #if {$found == 0} {
                #regex %%1 {$fromdir[$lastdir]} {
                    #showme going %%2;
                    #var lastdir %%2;
                    %%2;
                } {
                    #showme going %%1;
                    #var lastdir %%1;
                    %%1;
                };
            };
        };
        #class mazewalk close;
        set brief 1;
        %1;
    };
};

#alias mazeexit-special {
    
    #map exit %1 command {
        #class mazewalk open;
        #var fromdir[north] south;
        #var fromdir[west] east;
        #var fromdir[east] west;
        #var fromdir[south] north;
        #var fromdir[up] down;
        #var fromdir[down] up;
        #var fromdir[northup] southdown;
        #var fromdir[westup] eastdown;
        #var fromdir[eastup] westdown;
        #var fromdir[southup] northdown;
        #var fromdir[northdown] southup;
        #var fromdir[westdown] eastup;
        #var fromdir[eastdown] westup;
        #var fromdir[southdown] northup;
        #var fromdir[southeast] northwest;
        #var fromdir[northwest] southeast;
        #var fromdir[southwest] northeast;
        #var fromdir[northeast] southwest;
        #var lastdir %2;
        #var found 0;
        
        #action {你逃跑失败。} {
            #delay 1 lastdir;
        } 1;
        #action {%3} {
            #showme <fca>到达目标地点：%4;
            #var found 1;
            #map goto %4;
            #unticker walkmaze;
            set brief 1;
            #class mazewalk kill;
        } 2;
        #action {这里明显的方向有 %%1、%%2 和 %%3。} {
            #showme <fca> 岔路自动停止;
            #var found 1;
            #map goto %4;
            #unticker walkmaze;
            set brief 1;
            #class mazewalk kill;
        } 3;
        #action {这里明显的方向有 %%1 和 %%2。} {
            #if {$found == 0} {
                #regex %%1 {$fromdir[$lastdir]} {
                    #showme going %%2;
                    #var lastdir %%2;
                    %%2;
                } {
                    #showme going %%1;
                    #var lastdir %%1;
                    %%1;
                };
            };
        };
        #class mazewalk close;
        set brief 3;
        %1;
        #ticker walkmaze {l} 0.6;
    };
};
#alias runyz {#map run {中央广场} {{0.2}};};
#alias runwd {#map run {武当广场} {{0.2}};};
#alias runxy {#map run {120} {{0.2}};};
#alias runly {#map run {230} {{0.2}};};
#alias runca {#map run {435} {{0.2}};};

#class mapper close;
