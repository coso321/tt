#class songjing open;
#var {songjing.after-handler} {ask-quest};

#alias {songjing.start} {
    #var location %1;
    #var book %2;
    #regexp %3 {%!*·%!*·%*(%*)} {
        #var chapter &1;
        #var chapter.page &2;
    };
    getbook;
};


#alias getbook {
    #map run 复真观二层;
    jie $book; 
    #map run $location;
    findpage;
};
    
#alias findpage {
    #var page 1;
    #class findpage open;
        #action {~^\e[0m\e[1;32m%%1(%%2)\e[0m} {
            #regexp %%1 $chapter {
                #if {%%2 == ${chapter.page}} {
                    #delay 1 chantpage $page;
                };
                #else {
                    #math page {$page - (%%2 - ${chapter.page})};
                    page $page;
                };
            } {
                #math page $page+10;
                page $page;
            };
        };
    #class findpage close;
    page 1;
};
    
#alias chantpage {
    #var line {};
    #class findpage kill;
    #class chantpage open;
        #action {^==%!{\ +}{[^ ]+}%!{\ +}==$} {
            #var line ${line}%%1;
        };
    #class chantpage close;
    page %1;
    #delay 3 {
        #showme chanting %1 $line;
        chanting %1 $line; 
    };
};

#action {$myname拿起了%*慢慢的诵唱：} {
    #class chantpage kill;
}; 

#action {你做完了冲虚道长布置的功课，可以回去向他要奖赏了。} {
    #map run 复真观二层;
    give jing to zhike;
    #delay 3 {
        #map run 武当广场;
        success-quest;
        ${songjing.after-handler};
        #delay 4 {#class songjing kill};
    };
}
     
#class songjing close;
