#class wdquest open;

#prompt {当前进度：%1} {当前进度：%1};
#ticker {looking} {l} {30};

#var equip-drop 0;

#alias {AQ} {ask-quest};
#alias {ask-quest} {
    ask chongxu about quest;
};

#alias {CQ} {cancel-quest};
#alias {cancel-quest} {
    ask chongxu about cancel;
};

#alias {SQ} {success-quest};
#alias {success-quest} {
    ask chongxu about success;
};

#alias {goto-liandan} {
    #re bot/wudang/liandan.tt;
    liandan-start;
};

#alias {goto-zhenfa} {
    #re bot/wudang/zhenfa.tt;
    zhenfa-start;
};

#alias {goto-caiqi} {
    #re bot/wudang/caiqi.tt;
    caiqi.start;
};

#alias {goto-songjing} {
    #re bot/wudang/songjing.tt;
    songjing.start {%1} {%2} {%3};
};

#alias {goto-xiake} {
    #re bot/wudang/xiake.tt;
    xiake.start;
};


#action {你向冲虚道长打听有关『quest』的消息。$} {
    #class waitfor-chongxu-drop kill;
    #class waitfor-chongxu-answer open;
    #action {冲虚道长说道：「武当三侠最近迷上了炼丹} {
        #showme 当前进度：炼丹;
        #class waitfor-chongxu-answer kill;
        goto-liandan;
    };
    #action {冲虚道长说道：「武当派以真武七截阵闻名天下} {
        #showme 当前进度：阵法;
        #class waitfor-chongxu-answer kill;
        goto-zhenfa;
    };

    #action {冲虚道长说道：「%*要在%%1熟读典籍，据说要诵唱%%2的%%3$} {
        #showme 当前进度：诵经;
        #class waitfor-chongxu-answer kill;
        #regex %%1 {{小园|竹林小院|小屋|金顶|太和宫|二天门|三天门|五老峰|三老峰|虎头岩}} {
            cancel-quest;
            ask-quest;
        } {
            goto-songjing {%%1} {%%2} {%%3};
        };
    };
    #action {冲虚道长说道：「道家炼气最讲究时辰和地点} {
        #showme 当前进度：采气;
        #class waitfor-chongxu-answer kill;
        goto-caiqi;
    };
    #action {冲虚道长说道：「山下双井子村的崔老汉似乎有点麻烦，你现在下山一趟帮他解决麻烦。」} {
        #showme 当前进度：侠客;
        #class waitfor-chongxu-answer kill;
        #if {${do-xiake} == 1} {
            goto-xiake;
        };
        #else {
            ask chongxu about cancel;
            ask chongxu about quest;
        };
    };
    #action {冲虚道长说道：「现在还没有新的工作可以分配给你} {
        #showme 当前进度：等待任务;
        #class waitfor-chongxu-answer kill;
        #var waiting.next waiting.continue;
        #map goto 武当广场;
        #delay 1 waiting.${waiting.action};
    };
    #action {冲虚道长说道：「%*还有任务在身} {
        #class waitfor-chongxu-answer kill;
        ask chongxu about cancel;
        ask chongxu about quest;
    };
    #action {冲虚道长说道：「我看你是机器人吧！」} {
        #showme 当前进度：等待fullme;
        #class waitfor-chongxu-answer kill;
        #var waiting.next waiting.wait;
        #map goto 武当广场;
        #delay 1 waiting.${waiting.action};
    };
    #action {冲虚道长说道：「%%1} {
        #class waitfor-chongxu-answer kill;
        #var new-quest %%1;
        #bell;
    }{9};
    #class waitfor-chongxu-answer close;
};

#act {你向冲虚道长打听有关『success』的消息。} {
    #class waitfor-chongxu-drop open;
        #act {^从冲虚道长身上掉了出来一件} {#math {equip-drop} {${equip-drop}+1};};
        #act {^从冲虚道长身上掉了出来一柄} {#math {equip-drop} {${equip-drop}+1};};
        #act {从冲虚道长身上掉了出%*孽龙丹} {eat dan};
    #class waitfor-chongxu-drop close;
};

#alias waiting.continue {
    #if {${sell-equip} == 1 && ${equip-drop} > 0} {
        #unticker {looking};
        #action {^荣宝斋 -} {
            #action {^扬州钱庄 -} {#delay 1 {cun 1 gold; #delay 3 {#map run {武当广场%*} {0.2}; #unaction {^扬州钱庄 -};}}};
            #action {^武当广场 -} {
                set brief 1;
                #delay 1 ask chongxu about quest;
                #unaction {^武当广场 -};
            };
            #var equip-drop 0;
            #delay 1 sell all; 
            #delay 3 #map run {扬州钱庄%*} {0.2};
            #unaction {^荣宝斋 -};
        };
        set brief 3;
        #map run {荣宝斋%*} {0.2};
    } {
        #action {^武当广场 -} {
            ask chongxu about quest;
            #ticker {looking} {l} {30};
            #unaction {^武当广场 -};
        };
        #delay 1 {l;#map run 武当广场;};
    };
};

#alias waiting.wait {
    #delay 1 waiting.${waiting.action};
};

#action {你一觉醒来，精神抖擞地活动了几下手脚。} {
    #class waiting kill;
    #class tmp open;
    #act {^膳房 -} {
        get zhou;
        get hulu;
        eat zhou;
        drink hulu;
        drop zhou;
        drop hulu;
    };
    #act {{你丢下一个清水葫芦。|你身上没有 hulu 这样东西。}} {
        ${waiting.next};
        #class tmp kill;
    };
    #class tmp close;
    #map run 14;
};

#alias {waiting.fadai} {getfood {#delay 10 waiting.continue}; };


#alias {waiting.dushu} {
    #var book %1;
    #class waiting open;
    #action {^客房 -} {
        #delay 1 {du $book for 50; yun regenerate};
    };
    #action {这里不是你能睡的地方} {reloadmap; #delay 3; run 12 0.2;};
    #action {你必须先用 enable 选择你要用的内功心法。} {
        #delay 10 sleep;
    };
    #action {你刚在三分钟内睡过一觉, 多睡对身体有害无益!} {
        #delay 10 {du $book for 50; yun regenerate}; 
    };
    #action {你身上没有 $book 这种东西} {#delay 10 sleep};
    #action {你略一凝神，精神看起来好多了。} {
        du $book for 50; yun regenerate;
    };
    #action {( 你的内力已经枯竭，不能施用内功。)} {
        sleep;
    };
    #action {你现在精力充沛} {du $book for 50; yun regenerate};
    #action {你的内力不够。} {sleep;};
    #class waiting close;
    #map run {12} {0.2};
};

#alias {waiting.lian} {
    #var skill %1;
    #class waiting open;
    #action {你使用的武器不对。} {unwield all; wield $skill; lian $skill 50; yun recover};
    #action {练%*必须空手} {unwield all; lian $skill 50; yun recover};
    #action {^客房 -} {
        #delay 1 {lian $skill 50; yun recover};
    };
    #action {这里不是你能睡的地方} {reloadmap; #delay 3; run 12 0.2;};
    #action {你刚在三分钟内睡过一觉, 多睡对身体有害无益!} {
        #delay 10 yun recover; 
    };
    #action {你深深吸了几口气，脸色看起来好多了。} {lian $skill 50; yun recover};
    #action {( 你的内力已经枯竭，不能施用内功。)} {
        sleep;
    };
    #action {你现在气力充沛} {lian $skill 50; yun recover};
    #action {你的内力不够。} {sleep;};
    #class waiting close;
    #map run {12} {0.2};
};
#alias {waiting.xue} {
    #var skill %1;
    #class waiting open;
    #action {│  读书写字                │literate                  │%*    │%d.%*       │-} {
        #if {"$skill" == "literate" && %%2 >= 50} {wdquest dushu shu 2};
        #else {n; xue %2 for $skill 50; yun regenerate; };
    };
    #action {你略一凝神，精神看起来好多了。} {xue %2 for $skill 50; yun regenerate};
    #action {^客房 -} {
        sleep;
    };
    #action {这里不是你能睡的地方} {reloadmap; #delay 3 #map run 12;};
    #action {你刚在三分钟内睡过一觉, 多睡对身体有害无益!} {
        #delay 10 {w;w;xue %2 for $skill 50; #delay 1 {e;e; }}
    };
    #action {你现在气力充沛} {lian $skill 50; yun recover};
    #action {你现在精力充沛} {sleep};
    #action {你的内力不够。} {e;e;};
    #action {( 你的内力已经枯竭，不能施用内功。)} {e;e;};
    #action {你的潜能已经发挥到极限了，没有办法再成长了。} {e;e};
    #class waiting close;
    cha;
};
#class wdquest close;
