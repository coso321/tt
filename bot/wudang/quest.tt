#class wdquest open;

#alias {AQ} {ask-quest};
#alias {ask-quest} {
    ask chongxu about quest;
};

#alias {CQ} {cancel-quest};
#alias {cancel-quest} {
    ask chongxu about cancel;
};

#alias {SQ} {success-quest};
#alias {success-quest} {
    eat liang;
    drink jiudai;
    ask chongxu about success;
};

#alias {goto-liandan} {
    #re bot/wudang/liandan.tt;
    liandan-start;
};

#alias {goto-zhenfa} {
    #re bot/wudang/zhenfa.tt;
    zhenfa-start;
};

#alias {goto-caiqi} {
    caiqi.start;
};

#alias {goto-songjing} {
    #re bot/wudang/songjing.tt;
    songjing.start {%1} {%2} {%3};
}


#action {你向冲虚道长打听有关『quest』的消息。$} {
    #class waitfor-chongxu-answer open;
    #action {冲虚道长说道：「武当三侠最近迷上了炼丹} {
        #class waitfor-chongxu-answer kill;
        goto-liandan;
    };
    #action {冲虚道长说道：「武当派以真武七截阵闻名天下} {
        #class waitfor-chongxu-answer kill;
        goto-zhenfa;
    };

    #action {冲虚道长说道：「%*要在%%1熟读典籍，据说要诵唱%%2的%%3$} {
        #class waitfor-chongxu-answer kill;
        #regex %%1 {{金顶|太和宫|竹林小院|二天门|三天门|小园|小屋|五老峰|三老峰|虎头岩}} {
            cancel-quest;
            ask-quest;
        } {
            goto-songjing {%%1} {%%2} {%%3};
        };
    };
    #action {冲虚道长说道：「道家炼气最讲究时辰和地点} {
        #class waitfor-chongxu-answer kill;
        #nop goto-caiqi;
        cancel-quest;
        ask-quest;
    };
    #action {冲虚道长说道：「现在还没有新的工作可以分配给你} {
        #class waitfor-chongxu-answer kill;
        #delay 5 ask-quest;
    };
    #action {冲虚道长说道：「道兄还有任务在身} {
        #class waitfor-chongxu-answer kill;
    };
    #action {冲虚道长说道：「我看你是机器人吧！」} {
        #class waitfor-chongxu-answer kill;
        #class wdquest kill;
    };
    #action {冲虚道长说道：「} {
        #class waitfor-chongxu-answer kill;
        cancel-quest;
        ask-quest;
    }{10};
    #class waitfor-chongxu-answer close;
};

#class wdquest close;
